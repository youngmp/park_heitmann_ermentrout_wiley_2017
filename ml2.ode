# original code from http://www.math.pitt.edu/~bard/bardware/tut/newstyle.html#mlsyn
# combinations of T1/T2 exc/inh coupling.

# Morris-Lecar reduced model 
v'=(iapp+gl*(vl-v)+gk*w*(vk-v)+gca*minf(v)*(vca-v)+g*eps*sp*(vsyn-v))/c
w'=lamw(v)*(winf(v)-w)
s'=alpha*k(v)*(1-s)-beta*s

vp'=(iapp2+gl*(vl-vp)+gk*wp*(vk-vp)+gca2*minf(vp)*(vca-vp) + g*eps*s*(vsyn2-vp))/c 
wp'=lamw2(vp)*(winf2(vp)-wp)
sp'=alpha*k(vp)*(1-sp)-beta*sp

minf(v)=.5*(1+tanh((v-v1)/v2))
winf(v)=.5*(1+tanh((v-v3)/v4))
lamw(v)=phi*cosh((v-v3)/(2*v4))
k(v)=1/(1+exp(-(v-vt)/vs))

winf2(v)=.5*(1+tanh((v-v32)/v42))
lamw2(v)=phi2*cosh((v-v32)/(2*v42))


param vk=-84,vl=-60,vca=120
param gk=8,gl=2,c=20
param v1=-1.2,v2=18
param vt=20,vs=2,alpha=1,beta=.03
param g=5

p eps=.001

# parms that change per set
# (default to 1e1e)
param vsyn=0,gca=4,v3=12,v4=17.4,phi=0.067,iapp=43.5
param vsyn2=0,gca2=4,v32=12,v42=17.4,phi2=0.067,iapp2=43.5

# p iapp=40 is closer to the snic, but
# iapp=42.5 # sufficiently TI with compatible period to TII
# for exc/inh coupling to identical neurons (C1 to C1 or C2 to C2), I use C1 iapp=42.5 and C2 iapp=88.5
# for mixed coupling, I use C1 iapp=43.5 and C2 iapp=88.5
# for mixed 12ii, use iapp2=88.9

# parameter sets (TI/II, exc/inh).
# changes: gca,v3,v4,iapp,vsyn,phi
# include gca2,v32,v42,iapp2,vsyn2,phi2
set 1e1e {iapp=43.5,v3=12,v4=17.4,gca=4,phi=.067,vsyn=0,iapp2=43.5,v32=12,v42=17.4,gca2=4,phi2=.067,vsyn2=0}
set 1e1i {iapp=43.5,v3=12,v4=17.4,gca=4,phi=.067,vsyn=0,iapp2=43.5,v32=12,v42=17.4,gca2=4,phi2=.067,vsyn2=-75}
set 1i1i {iapp=43.5,v3=12,v4=17.4,gca=4,phi=.067,vsyn=-75,iapp2=43.5,v32=12,v42=17.4,gca2=4,phi2=.067,vsyn2=-75}

set 1e2e {iapp=43.5,v3=12,v4=17.4,gca=4,phi=.067,vsyn=0,iapp2=88.5,v32=2,v42=30,gca2=4.4,phi2=.04,vsyn2=0}
set 1i2i {iapp=43.5,v3=12,v4=17.4,gca=4,phi=.067,vsyn=-75,iapp2=88.5,v32=2,v42=30,gca2=4.4,phi2=.04,vsyn2=-75}

set 1i2e {iapp=43.5,v3=12,v4=17.4,gca=4,phi=.067,vsyn=0,iapp2=88.5,v32=2,v42=30,gca2=4.4,phi2=.04,vsyn2=-75}
set 1e2i {iapp=43.5,v3=12,v4=17.4,gca=4,phi=.067,vsyn=-75,iapp2=88.5,v32=2,v42=30,gca2=4.4,phi2=.04,vsyn2=0}

set 2e2e {iapp=88.5,v3=2,v4=30,gca=4.4,phi=.04,vsyn=0,iapp2=88.5,v32=2,v42=30,gca2=4.4,phi2=.04,vsyn2=0}
set 2i2e {iapp=88.5,v3=2,v4=30,gca=4.4,phi=.04,vsyn=0,iapp2=88.5,v32=2,v42=30,gca2=4.4,phi2=.04,vsyn2=-75}
set 2i2i {iapp=88.5,v3=2,v4=30,gca=4.4,phi=.04,vsyn=-75,iapp2=88.5,v32=2,v42=30,gca2=4.4,phi2=.04,vsyn2=-75}


# TI membrane + TI PRC at bifurcation
# start v,w,s at phase 0 (max v)
# start vp,wp,sp at phase 1/4 (time 33.0625 of 132.25) # get new init for new choice of i, if needed.
# class 1 PRC with i=43.5

#v(0)=30.48642
#w(0)=.228588
#s(0)=.8890991
#vp(0)=-42.66515
#wp(0)=.01201348
#sp(0)=.2279906


# TII membrane + TII PRC at bifurcation
# start v,w,s at phase 0 (max v)
# start vp,wp,sp at phase 1/4 (28.6375 of 114.55)
# cass 2 PRC with i=88.5
#v(0)=28.75165
#w(0)=.3309653
#s(0)=.9201432
#vp(0)=-52.09383
#wp(0)=.2830268
#sp(0)=.3245806


# 12/21 ee/ei/ie/ii
# start v,w,s at phase 1/4 of TI
# start vp,wp,sp at phase 0 (max V) of TII
# cass 2 PRC with i=88.5
v(0)=-42.66515
w(0)=.01201348
s(0)=.2279906
vp(0)=28.75165
wp(0)=.3309653
sp(0)=.9201432



@ total=200000
@ maxstor=10000000
done