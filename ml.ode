# from http://www.math.pitt.edu/~bard/bardware/tut/newstyle.html#mlsyn

# Morris-Lecar reduced model 
v'=(i+gl*(vl-v)+gk*w*(vk-v)+gca*minf(v)*(vca-v))/c
w'=lamw(v)*(winf(v)-w)
s'=alpha*k(v)*(1-s)-beta*s

# in the full system I add
# g*eps*sp*(vsyn-v))/c 
# to the RHS of v'

minf(v)=.5*(1+tanh((v-v1)/v2))
winf(v)=.5*(1+tanh((v-v3)/v4))
lamw(v)=phi*cosh((v-v3)/(2*v4))
k(v)=1/(1+exp(-(v-vt)/vs))

param vk=-84,vl=-60,vca=120
param gk=8,gl=2,c=20
param v1=-1.2,v2=18
param vt=20,vs=2,alpha=1
# default beta (slow synapse,default)
param beta=0.03

# old beta (fast synapse)
# param beta=0.25
param g=5

# parms that change per set
# (default to TI (SNIC), exc)
param vsyn=0,gca=4,v3=12,v4=17.4,phi=0.067,i=43.5
# p=40 is closer to the snic, but
# i=42.5 # sufficiently TI with compatible period to TII
# i=43.5 # has an almost identical period to TII at i=88.5. period 114.85

# parameter sets (TI/II, exc, inh)
set ti_exc {i=43.5,v3=12,v4=17.4,gca=4,phi=.067,vsyn=0}
set ti_inh {i=43.5,v3=12,v4=17.4,gca=4,phi=.067,vsyn=-75}
set tii_exc {i=88.5,v3=2,v4=30,gca=4.4,phi=.04,vsyn=0}
set tii_inh {i=88.5,v3=2,v4=30,gca=4.4,phi=.04,vsyn=-75}

#TI membrane init
# class 1 PRC with i=42.5, LC period 132.25
# class 1 PRC with i=43.5, LC period 114.85
#v(0)=30.48642
#w(0)=.228588
#s(0)=.8890991
##@ total=132.25
#@ total=114.85

#TII membrane init
# class 2 PRC (hopf bifurcation) at i=88.5, LC period 114.55
v(0)=28.75165
w(0)=.3309653
s(0)=.9201432
@ total=114.55
# at i=88.9, total=109.1

@ maxstor=1000000

done
